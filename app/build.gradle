plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.google.gms.googleServices)
    id "kotlin-kapt"
    alias(libs.plugins.google.dagger.hilt)
}

def TMDB_ACCESS_TOKEN = System.getenv("TMDB_ACCESS_TOKEN")
if (TMDB_ACCESS_TOKEN == null)
    throw new IllegalArgumentException("TMDB_ACCESS_TOKEN must be declared!")

android {
    namespace 'com.example.top5movies'
    compileSdk 34

    defaultConfig {
        applicationId "com.example.top5movies"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "TMDB_ACCESS_TOKEN", "\"$TMDB_ACCESS_TOKEN\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.viewmodel
    implementation libs.androidx.activity.compose
    implementation libs.androidx.navigation.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.androidx.ui.text.google.fonts
    implementation platform(libs.google.firebase.bom)
    implementation libs.google.firebase.auth
    implementation libs.google.firebase.firestore
    implementation libs.google.dagger.hilt
    kapt libs.google.dagger.hilt.kapt
    implementation libs.androidx.hilt.navigation.compose
    implementation libs.io.coil
    implementation libs.io.coil.compose
    implementation libs.bundles.retrofit

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}

kapt {
    correctErrorTypes = true
}